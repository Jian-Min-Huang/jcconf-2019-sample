buildscript {
    repositories {
        mavenCentral()
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${versions.springBoot}")
        classpath("se.transmode.gradle:gradle-docker:${versions.gradleDocker}")
    }
}

apply plugin: "java"
apply plugin: "idea"
apply plugin: "docker"
apply plugin: "org.springframework.boot"
apply plugin: "io.spring.dependency-management"

group = "org.yfr"
version = "0.0.1"
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url "https://repo.spring.io/snapshot" }
    maven { url "https://repo.spring.io/milestone" }
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:Greenwich.SR2"
    }
}

dependencies {
    compile("org.springframework.cloud:spring-cloud-starter-gateway")
    compile("org.springframework.cloud:spring-cloud-starter-netflix-hystrix")
    compile("org.springframework.cloud:spring-cloud-starter-contract-stub-runner"){
        exclude group: "org.springframework.boot", module: "spring-boot-starter-web"
    }
}

task buildDocker(type: Docker) {
    baseImage = "openjdk:8-jre-slim"
    tag = "jcconf-2019-sample/sample-gw"
    addFile {
        from jar
        rename { "app.jar" }
    }
    entryPoint(['java', '-Xms512m', '-Xmx512m', '-Dspring.profiles.active=${activeProfiles}', '-jar', 'app.jar'])
}
buildDocker.dependsOn(build)
